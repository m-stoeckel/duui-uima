export VERSION := "0.3.0"
export CUDA_VERSION := "cu124"
export CUDA_IMAGE_124 := "12.4.1-cudnn-devel-ubuntu22.04"
export CUDA_IMAGE_122 := "12.2.2-cudnn8-devel-ubuntu22.04"
export LLAMA_VERSION := "0.2.69"
export DEPS_VERSION := "12.4.1-0.2.69"
export REPOSITORY := "docker.texttechnologylab.org/llm/llama-cpp-python"

default tag=VERSION:
    @just hf
    @just deps-cu122 {{tag}}
    @just deps-cu124 {{tag}}
    @just base {{tag}} cu122
    @just base {{tag}} cu124
    @just variants {{tag}} {{tag}} cu122
    @just variants {{tag}} {{tag}} cu124


hf version="latest":
    docker build -t "docker.texttechnologylab.org/llm/hf:{{version}}" -f src/main/docker/Dockerfile.hf .
    docker tag "docker.texttechnologylab.org/llm/hf:{{version}}" "docker.texttechnologylab.org/llm/hf:latest"
    docker push "docker.texttechnologylab.org/llm/hf:{{version}}"
    docker push "docker.texttechnologylab.org/llm/hf:latest"


_deps tag=VERSION image=CUDA_IMAGE_124 cuda=CUDA_VERSION llama=LLAMA_VERSION:
    docker build \
        --build-arg CUDA_IMAGE_124="{{image}}" \
        --build-arg LLAMA_CPP_PYTHON_VERSION="{{llama}}" \
        -t "{{REPOSITORY}}/{{cuda}}/deps:llama-{{llama}}" \
        --push \
        -f src/main/docker/Dockerfile.deps .
    docker image tag "{{REPOSITORY}}/{{cuda}}/deps:llama-{{llama}}" "{{REPOSITORY}}/{{cuda}}/deps:{{tag}}"
    docker push "{{REPOSITORY}}/{{cuda}}/deps:{{tag}}"


# build dependency image for CUDA 12.2.2
deps-cu122 tag=VERSION llama=LLAMA_VERSION:
    just _deps {{tag}} {{CUDA_IMAGE_122}} cu122 {{llama}}
    
# build dependency image for CUDA 12.4.1
deps-cu124 tag=VERSION llama=LLAMA_VERSION:
    just _deps {{tag}} {{CUDA_IMAGE_124}} cu124 {{llama}}

alias deps: deps-cu124

# build base image, tagged 'tag' and 'latest', from dependency image 'deps'
base tag=VERSION cuda=CUDA_VERSION llama=LLAMA_VERSION:
    docker build \
        --build-arg CUDA_VERSION="{{cuda}}" \
        --build-arg LLAMA_VERSION="{{llama}}" \
        -t "{{REPOSITORY}}/{{cuda}}/base:{{tag}}" \
        --push \
        -f src/main/docker/Dockerfile.base .
    docker image tag "{{REPOSITORY}}/{{cuda}}/base:{{tag}}" "{{REPOSITORY}}/{{cuda}}/base:latest"
    docker push "{{REPOSITORY}}/{{cuda}}/base:latest"


variant variant tag=VERSION base=VERSION cuda=CUDA_VERSION:
    docker build \
        --build-arg CUDA_VERSION="{{cuda}}" \
        --build-arg BASE_VERSION="{{base}}" \
        -t "{{REPOSITORY}}/{{cuda}}/var/{{variant}}:{{tag}}" \
        --push \
        src/variants/{{variant}}/
    docker image tag "{{REPOSITORY}}/{{cuda}}/var/{{variant}}:{{tag}}" "{{REPOSITORY}}/{{cuda}}/var/{{variant}}:latest"
    docker push "{{REPOSITORY}}/{{cuda}}/var/{{variant}}:latest"


example tag=VERSION base=VERSION cuda=CUDA_VERSION:
    just variant "tinyllama-1.1b" {{tag}} {{base}} {{cuda}}


variants tag=VERSION base=VERSION cuda=CUDA_VERSION:
    #!/bin/bash
    for variant in src/variants/*; do \
        just variant "$(basename $variant)" {{tag}} {{base}} {{cuda}}; \
    done
