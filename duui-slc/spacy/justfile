set allow-duplicate-recipes := true
set allow-duplicate-variables := true

import? '../base/justfile'

export CUDA_NAME := "cu124"
export CUDA_VERSION := "12.4.1"

export VERSION := "0.5.1"

export REPOSITORY := "docker.texttechnologylab.org"
export ANNOTATOR_NAME := "duui-slc-spacy"

build version=VERSION:
    just base {{version}}
    docker build \
        --build-arg CUDA_NAME="{{CUDA_NAME}}" \
        --build-arg CUDA_VERSION="{{CUDA_VERSION}}" \
        --build-arg VERSION="{{VERSION}}" \
        -t {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}} \
        -f src/main/docker/Dockerfile src/main/

test version=VERSION:
    docker container ls | grep {{ANNOTATOR_NAME}}-test && docker stop {{ANNOTATOR_NAME}}-test && docker rm {{ANNOTATOR_NAME}}-test || echo ""
    docker run --rm --name {{ANNOTATOR_NAME}}-test -d -p 9714:9714 {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}}
    sleep 2 && mvn test || echo "Tests failed"
    docker stop {{ANNOTATOR_NAME}}-test

tag version=VERSION as="latest":
    docker tag {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}} {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:latest

push version=VERSION:
    docker push {{REPOSITORY}}/{{ANNOTATOR_NAME}}/{{CUDA_NAME}}:{{version}}
